<?page
title=>Invite Codes
body<=
<?_code
{
    use strict;
    
    return LJ::server_down_html() if ($LJ::SERVER_DOWN);
    
    my $remote = LJ::get_remote();
    return "<?needlogin?>"
        unless $remote;
    
    my $ret;

    if ( LJ::did_post() ) {
        return "<?h1 $ML{'Error'} ?h1> $ML{'error.invalidform'}" unless LJ::check_form_auth();
        if ( DW::InviteCodeRequests->create( userid => $remote->id, reason => $POST{reason} ) ) {
            $ret .= "<?standout $ML{'.msg.request.success'} standout?>";
        } else {
            $ret .= "<?errorbar $ML{'.msg.request.error'} errorbar?>";
        }
    }

    if ( DW::BusinessRules::InviteCodeRequests::can_request( user => $remote ) ) {
        $ret .= "<?h2 $ML{'.form.request.header'} h2?>";
        $ret .= "<?p $ML{'.form.request.intro'} p?>";
        $ret .= "<div><form method='POST'>";
        $ret .= LJ::form_auth();
        $ret .= LJ::labelfy( 'reason', $ML{'.form.request.reason'} );
        $ret .= LJ::html_text( { name => 'reason', id => 'reason', size => 75, maxlength => 255  } );
        $ret .= LJ::html_submit( value => $ML{'.form.request.submit'} );
        $ret .= "</form></div>";
    }

    if ( $GET{full} ) {
        $ret .= "<p>" . BML::ml( '.label.viewing.full', { aopts => "href='invitecodes'" } ) . "</p>";
    } else {
        $ret .= "<p>" . BML::ml( '.label.viewing.partial', { aopts => "href='invitecodes?full=1'" } ) . "</p>";
    }

    my @invitecodes = DW::InviteCodes->by_owner( userid => $remote->id );

    my @recipient_ids;
    foreach my $code ( @invitecodes ) {
        push @recipient_ids, $code->recipient if $code->recipient;
    }
    
    my $recipient_users = LJ::load_userids( @recipient_ids );

    unless ( $GET{full} ) {
        # filter out codes that were used over two weeks ago
        my $two_weeks_ago = time() - ( 14 * 24 * 60 * 60 );
        @invitecodes = grep { 
            my $u = $recipient_users->{$_->recipient};
            # if it's used, we should always have a recipient, but...
            ! $_->is_used || ( $u && $u->timecreate ) > $two_weeks_ago
        } @invitecodes;
    }
    
    if ( @invitecodes ) {

        # sort so that invite codes end up in this order:
        #  - unsent and unused
        #  - sent but unused, with earliest sent first
        #  - used
        @invitecodes = sort { 
            return $a->is_used <=> $b->is_used if $a->is_used != $b->is_used;
            return $a->timesent <=> $b->timesent;
        } @invitecodes;


        $ret .= "<table class='invitecodes' id='invitecodes'><tr><th>$ML{'.header.code'}</th><th>$ML{'.header.recipient'}</th><th width='200'>$ML{'.header.sent'}</th><th>$ML{'.header.email'}</th></tr>";
    
        foreach my $code ( @invitecodes ) {
            $ret .= "<tr>";
            $ret .= "<td><tt>".$code->code."</tt></td>";
            $ret .= "<td>";
        
            if( $code->is_used ) {
                my $u = $recipient_users->{$code->recipient};
                $ret .= $u->ljuser_display;
            } else {
                my $create_link = ($LJ::USE_SSL ? $LJ::SSLROOT : $LJ::SITEROOT)
                    . "/create.bml?from=$remote->{user}&code=".$code->code;
                $ret .= BML::ml( '.code.use', { aopts => "href='$create_link'" } );
            }        

            $ret .= "</td>";
            
            $ret .= "<td>";
            $ret .= LJ::time_to_http( $code->timesent ) if $code->timesent;
            $ret .= "</td>";
            
            $ret .= "<td>";
            $ret .= $code->email;
            $ret .= "</td>";
            
            $ret .= "</tr>";
        }
    
        $ret .= "</table>";
    } elsif ( $GET{full} ) {
        $ret .= "<?p $ML{'.noinvitecodes'} p?>";
    } else {
        $ret .= "<?p $ML{'.noinvitecodes.partial'} p?>";
   }

    return $ret;
} _code?>
<=body
page?>
