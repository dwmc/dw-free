<html>
<head><title>Payment Management</title></head>
<body>

<?_code
{
    use strict;
    use vars qw(%GET %POST);

    my $remote = LJ::get_remote();
    my @displayprivs = ( "payments" );
    my $numprivs = @displayprivs;

    return "<?needlogin?>" unless $remote; 
    return BML::ml( "admin.noprivserror", { numprivs => $numprivs, needprivs => "<b>" . join(", ", @displayprivs) . "</b>"} )
        unless $remote->has_priv( 'payments' );

    my $body = '<h1>Payment Manager</h1>';

    if ( LJ::did_post() ) {
        return "failed auth"
            unless LJ::check_form_auth();

        if ( $POST{givetime} ) {
            my ( $who, $type, $months, $days, $datetime ) = ( $POST{user}, $POST{type}, $POST{months}, $POST{days}, $POST{datetime} );
            my $u = LJ::load_user( $who )
                or return "invalid user";

            if ( $POST{submit} eq 'Edit' ) {
              return "Invalid date/time format" unless $datetime =~ /^\d\d\d\d\-\d\d\-\d\d *( \d\d(:\d\d){1,2})?$/;
              my $rv = DW::Pay::edit_expiration_datetime( $u, $datetime );
              if ( $rv ) {
                  LJ::statushistory_add( $u, $remote, 'paidstatus',
                                         "Admin override: edit expiration date/time: $datetime" );
                  return BML::redirect( "$LJ::SITEROOT/admin/pay/index?view=$u->{user}" );
              }
              return "Error: " . DW::Pay::error_text() . "\n";
            } 

            return "invalid type"
                unless $type =~ /^(?:seed|premium|paid|expire)$/;
            $months = 0 if $months eq '';
            $days = 0 if $days eq '';
            $months = 99 if $type eq 'seed';

            if ( $type eq 'expire' ) {
                my $rv = DW::Pay::expire_user( $u, force => 1 );
                if ( $rv ) {
                    LJ::statushistory_add( $u, $remote, 'paidstatus',
                                           "Admin override: expired account." );
                    return BML::redirect( "$LJ::SITEROOT/admin/pay/index?view=$u->{user}" );
                }
                    
            } else {
                my $rv = DW::Pay::add_paid_time( $u, $type, $months, $days );
                if ( $rv ) {
                    LJ::statushistory_add( $u, $remote, 'paidstatus',
                                           "Admin override: gave paid time to user: months=$months days=$days type=$type" );
                    return BML::redirect( "$LJ::SITEROOT/admin/pay/index?view=$u->{user}" );
                }
            }
            return "Error: " . DW::Pay::error_text() . "\n";
        }

        return "invalid option";
    }

    if ($GET{view}) {
        $body .= '<p>[ <a href="/admin/pay/index">&lt;&lt; Back to Index</a> ]</p>';

        # allow editing a user's paid status
        my $u = LJ::load_user( $GET{view} );
        return "User does not exist.\n"
            unless $u;

        my $ps = DW::Pay::get_paid_status( $u );
        my $body_date = '';
        if ( $ps ) {
            $body .= '<h2>Paid Status</h2>';
            $body .= LJ::ljuser( $u ) . ": " . DW::Pay::type_name( $ps->{typeid} );

            if ( $ps->{permanent} ) {
                $body .= '; <strong>Permanent Status</strong> will never expire.';

            } else {
                if ( $ps->{expiresin} > 0 ) {
                    my $expt = LJ::mysql_time( $ps->{expiretime} );
                    my $exp = LJ::ago_text( $ps->{expiresin} );
                    $exp =~ s/ ago//;
                    $body .= "; expires $expt (<strong>$exp</strong>).";
                    $body_date = <<EOF;

<tr><td>Or edit expiration date:</td>
<td align="center"><input type="text" name="datetime" maxlength="20" size="20" value="$expt"/></td>
<td>    <input type="submit" name="submit" value="Edit" /></td></tr>
EOF
                } else {
                    $body .= '; <strong>expired</strong>.';
                }
            }
        } else {
            $body .= '<p>User has never had a paid account of any kind.</p>';
        }

        # give the box for adding paid time to the user
        my $auth = LJ::form_auth();
        $body .= <<EOF;
<br /><br />
    <form method="post"><input type="hidden" name="givetime" value="1">
    <input type="hidden" name="user" value="$u->{user}">$auth
<table summary='' border='0' cellpadding='4'>
<tr><td>Give Paid Time:
    <select name="type">
        <option value="paid">Paid Account</option>
        <option value="premium">Paid Premium Account</option>
        <option value="seed">Seed Account</option>
        <option value="blank"></option>
        <option value="expire">FORCE EXPIRATION</option>
    </select></td>
<td align="center">
        for <input type="text" name="months" maxlength="2" size="3" /> months, 
        <input type="text" name="days" maxlength="2" size="3" /> days
</td>
<td><input type="submit" name="submit" value="Give!" /></td></tr>
$body_date
</table>
</form>
<p><a href="/admin/statushistory?user=$u->{user}">View statushistory for user.</a></p>
<h2>View Carts</h2>
EOF

        my @carts = DW::Shop::Cart->get_all( $u );

        if ( @carts ) {
            $body .= "<table border='1'>";
            $body .= "<thead><tr><th>Cart Number</th><th>Date</th><th>Total</th>";
            $body .= "<th>Payment Method</th><th>Status</th><th>Details</th></tr></thead>";
            foreach my $cart ( @carts ) {
                my $state = $cart->state;
                my $paymentmethod = $cart->paymentmethod;
                my $paystr = ( $paymentmethod ?
                                   $ML{"/shop/receipt.bml.cart.paymentmethod.$paymentmethod"} :
                                   "(not yet selected)" );
                my $date = DateTime->from_epoch( epoch => $cart->starttime );

                my $detailstext = "Details";
                $detailstext .= " / Mark as Payment Received" if $state == $DW::Shop::STATE_PEND_PAID;

                $body .= "<tr>";
                $body .= "<td>" . $cart->id . "</td>";
                $body .= "<td>" . $date->strftime( "%F %r %Z" ) . "</td>";
                $body .= "<td>" . $cart->display_total . "</td>";
                $body .= "<td>$paystr</td>";
                $body .= "<td>$ML{\"/shop/receipt.bml.cart.status.$state\"}</td>";
                $body .= "<td><a href='$LJ::SITEROOT/admin/pay/view?cartid=" . $cart->id . "'>$detailstext</a></td>";
                $body .= "</tr>";
            }
            $body .= "</table>";
        }
        else {
            $body .= "<p>This user has not made any orders.</p>";
        }

    } else {
        # print the basic form
        $body .= <<EOF;

<ul>
    <li>Edit user:
      <form method="get" action="/admin/pay/index">
      <input type="text" name="view" /> <input type="submit" value="Go" />
      </form>
    </li>
    <li>View cart/order ID (or PayPal transaction ID):
      <form method="get" action="/admin/pay/view">
      <input type="text" name="cartid" /> <input type="submit" value="Go" />
      </form>
    </li>
    <li>View invite code:
      <form method="get" action="/admin/pay/view">
      <input type="text" name="code" /> <input type="submit" value="Go" />
      </form>
    </li>
</ul>

EOF

    }

    return $body;
}
_code?>

</body>
</html>
