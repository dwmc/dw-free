<?_info
nocache=>1
_info?><?page
title=><?_code return $FORM{'id'} ? "Edit FAQ Item #$FORM{'id'}" : "Add to FAQ"; _code?>
body<=
<center>
<a href="./"><b>(Back to FAQ Index)</b></a>
</center>

<?_code
{
    use strict;

    # FIXME: add crumb

    my $id = $FORM{id} + 0;
    my $ret = "";

    my $dbh = LJ::get_db_writer();

    my $remote = LJ::get_remote();
    my %ac_edit;
    my %ac_add;
    LJ::remote_has_priv( $remote, "faqadd", \%ac_add );
    LJ::remote_has_priv( $remote, "faqedit", \%ac_edit );
    my $faqd = LJ::Lang::get_dom( "faq" );
    my $rlang = LJ::Lang::get_root_lang( $faqd );
    my ( $faqcat, $sortorder, $question, $summary, $answer, $has_summary );

    if ( $id != 0 ) {
        my $faq = LJ::Faq->load( $id, lang => $rlang->{lncode} )
            or return "<b>Error:</b> FAQ #$id does not exist.";

        $faqcat = $faq->faqcat;
        $sortorder = $faq->sortorder;
        $question = $faq->question_raw;
        $summary = $faq->summary_raw;
        $answer = $faq->answer_raw;
        $has_summary = $faq->has_summary;
        unless ( $ac_edit{'*'} || $ac_edit{$faqcat} ) {
            if ( %ac_edit ) {
                return "<b>Error: </b> You do not have access to edit a FAQ question in the \"$faqcat\" category.";
            } else {
                return "<b>Error: </b> You do not have access to edit the FAQ.";
            }
        }
    } else {
        return "<b>Error: </b> You do not have access to add to the FAQ."
            unless %ac_add;
    }

    $sortorder += 0;
    $sortorder ||= 50;

    if ( LJ::did_post() ) {
        if ( $POST{'action:save'} ) { # Save FAQ
            return "<b>$ML{'Error'}</b> $ML{'error.invalidform'}"
                unless LJ::check_form_auth();

            my $opts_question = { changeseverity => $FORM{sev_question}+0 };
            my $opts_summary = { changeseverity => $FORM{sev_summary}+0 };
            my $opts_answer = { changeseverity => $FORM{sev_answer}+0 };
            my $do_trans = sub {
                my $id = shift;
                return unless $faqd;
                LJ::Lang::set_text( $dbh, $faqd->{dmid}, $rlang->{lncode},
                                    "$id.1question", $FORM{q}, $opts_question );
                LJ::Lang::set_text( $dbh, $faqd->{dmid}, $rlang->{lncode},
                                    "$id.3summary", $FORM{s}, $opts_summary )
                    if LJ::is_enabled( 'faq_summaries' );
                LJ::Lang::set_text( $dbh, $faqd->{dmid}, $rlang->{lncode},
                                    "$id.2answer", $FORM{a}, $opts_answer );
            };

            if ( $id == 0 ) {
                $dbh->do( qq{ INSERT INTO faq
                              ( faqid, question, summary, answer, faqcat,
                                sortorder, lastmoduserid, lastmodtime)
                              VALUES (NULL, ?, ?, ?, ?, ?, ?, NOW()) },
                          undef, @FORM{qw( q s a faqcat )},
                          $FORM{sortorder}+0 || 50, $remote->{userid} );
                $id = $dbh->{mysql_insertid};
                $ret .= $dbh->errstr || "Added FAQ item. All good. FAQ id is <b><a href='$LJ::SITEROOT/support/faqbrowse.bml?faqid=$id'>$id</a></b>";

                $opts_question->{childrenlatest} = 1;
                $opts_summary->{childrenlatest} = 1;
                $opts_answer->{childrenlatest} = 1;
                $do_trans->( $id ) if $id;
            } elsif ( $FORM{q} =~ /\S/ ) {
                my $sortorder = $FORM{sortorder}+0 || 50;
                # FIXME: don't overwrite existing summary if missing in form
                $dbh->do( qq{ UPDATE faq SET question=?, summary=?, answer=?,
                                             faqcat=?, lastmoduserid=?,
                                             lastmodtime=NOW(), sortorder=?
                                         WHERE faqid=? },
                          undef, @FORM{qw( q s a faqcat )}, $remote->{userid},
                          $FORM{sortorder}+0 || 50, $id );
                $ret .= "Updated FAQ item. All good. FAQ id is <b><a href='$LJ::SITEROOT/support/faqbrowse.bml?faqid=$id'>$id</a></b>";

                $do_trans->( $id );
            } else {
                $dbh->do( "DELETE FROM faq WHERE faqid=?", undef, $id );
                $ret .= "FAQ item deleted.";

                # FIXME: delete translation from ml_* ?
            }

            return $ret;
        }
    }

    $ret .= "<form action='faqedit.bml' method='post'>";
    $ret .= LJ::form_auth();
    $ret .= LJ::html_hidden( 'id', $id );
    $ret .= "<p>Category: ";

    my $sth = $dbh->prepare( "SELECT faqcat, faqcatname FROM faqcat ORDER BY catorder" );
    $sth->execute;
    my @cats;
    push @cats, '', '';

    while ( my ($fc, $fcname) = $sth->fetchrow_array ) {
        # $fc can't be NULL/undef
        push @cats, $fc, $fcname
            if $ac_add{'*'} || $ac_add{$fc} || $fc eq $faqcat;
    }

    $ret .= LJ::html_select( { name => 'faqcat', selected => $faqcat }, @cats );

    $ret .= "&nbsp;SortOrder: ";
    $ret .= LJ::html_text( { name => 'sortorder', value => $sortorder,
                             size => 5, maxlength => 4 } );

    $ret .= "<br><i>(sort order is how to sort within the category.  categories themselves are also sorted.)</i></p>";

    $ret .= "<?h1 Question h1?> <?p (as brief as possible, do not span multiple lines) p?><p>";

    $ret .= LJ::html_textarea( { name => 'q', value => $question,
                                 rows => 2, cols => 70, wrap => 'soft' } );
    $ret .= "<br /><i>(erase question to delete FAQ entry)</i></p>";

    if ( $faqd && $id != 0 ) {
        $ret .= "<p><b>Select modification level for question:</b> ";
        $ret .= LJ::html_select( { name => "sev_question", selected => 0 },
                                 0 => "Typo/etc (no notify)",
                                 1 => "Minor (notify translators)",
                                 2 => "Major (require translation updates)" );
        $ret .= "</p>";
    }

    if ( LJ::is_enabled( 'faq_summaries' ) || $has_summary ) {
        # If FAQ has summary and summaries are disabled, leave field in, but
        # make it read-only to let FAQ editors copy from it.
        my $readonly = !LJ::is_enabled( 'faq_summaries' );
        $ret .= "<?h1 Summary h1?> <?p (should be a shortish paragraph, urls are automatically linked, same markup as journal entries, no lj-cut) p?><p>";

        $ret .= LJ::html_textarea( { name => 's', value => $summary,
                                     rows => '10', cols => '70',
                                     wrap => 'soft', disabled => $readonly } );
        $ret .= "</p>";

        if ( $faqd && $id != 0 && !$readonly ) {
            # If absent, severity will default to 0 (unchanged), which is
            # the right thing for a readonly field.
            $ret .= "<p><b>Select modification level for summary:</b> ";
            $ret .= LJ::html_select( { name => "sev_summary", selected => 0 },
                                     0 => "Typo/etc (no notify)",
                                     1 => "Minor (notify translators)",
                                     2 => "Major (require translation updates)" );
            $ret .= "</p>";
        }
    }

    $ret .= "<?h1 Answer h1?> <?p (long as you want, urls are automatically linked, same markup as journal entries, no lj-cut) p?><p>";

    $ret .= LJ::html_textarea( { name => 'a', value => $answer,
                                 rows => '15', cols => '70', wrap => 'soft' } );
    $ret .= "</p>";

    if ( $faqd && $id != 0 ) {
        $ret .= "<p><b>Select modification level for answer:</b> ";
        $ret .= LJ::html_select( { name => "sev_answer", selected => 0 },
                                 0 => "Typo/etc (no notify)",
                                 1 => "Minor (notify translators)",
                                 2 => "Major (require translation updates)" );
        $ret .= "</p>";
    }

    $ret .= "<p>" . LJ::html_submit( 'action:save', 'Add/Edit FAQ Item' )
            . "</p></form>";
    return $ret;
}
_code?>

<=body
page?><?_c <LJDEP>
lib: cgi-bin/ljlib.pl
post: htdocs/admin/faq/faqedit_do.bml
</LJDEP> _c?>
