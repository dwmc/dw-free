<?_code
{
    use strict;
    use vars qw($title $body $bodyopts $head %GET %POST);
    use LJ::Portal::Config;

    my $disabled =  $LJ::DISABLED{'portal'};

    my $remote = LJ::get_remote();

    if (ref $disabled eq "CODE") {
        $disabled = $disabled->($remote);
    }

    if ($disabled) {
        $title = 'Disabled';
        $body = 'Sorry, the portal is disabled at this time.';
        return;
    }

    my $remote = LJ::get_remote;

    $title = BML::ml('.pagetitle', { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
    $body = '';
    $bodyopts = '';

    # browser caching causes lots of issues
    $head = '<META HTTP-EQUIV="CACHE-CONTROL" CONTENT="NO-CACHE">';


    if (!$remote) {
        $body = "<?needlogin?>";
        return;
    }

    my $returnjs = sub {
        my $response = shift;
        BML::set_content_type('text/javascript; charset=utf-8');
        BML::finish();
        BML::noparse();
        return $response;
    };

    # include the default style:
    LJ::need_res('stc/portal/MainStyle.css');
    # lj base css
    LJ::need_res('stc/lj_base.css');
    # javascript crap
    LJ::need_res('js/core.js');
    LJ::need_res('js/dom.js');
    LJ::need_res('js/hourglass.js');
    LJ::need_res('js/x/x_core.js');
    LJ::need_res('js/x/x_dom.js');
    LJ::need_res('js/portal.js');
    LJ::need_res('js/x/x_event.js');
    LJ::need_res('js/commentmanage.js');

    # get the current scheme and load the correct style
    my $scheme = BML::get_scheme();
    if ($scheme eq 'dystopia' && -e "$LJ::HOME/htdocs/stc/portal/Dystopia.css") {
        LJ::need_res('stc/portal/Dystopia.css');
    } elsif (-e "$LJ::HOME/htdocs/stc/portal/XColibur.css") {
        LJ::need_res('stc/portal/XColibur.css');
    }

    #-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

    # this is the url to do XML HTTP posts to
    my $selflink = $LJ::SITEROOT . BML::get_uri();
    $selflink =~ s/\?.*//;

    # fading special effects?
   my $doFadeDefault = 1;
    my $doFade = $LJ::DISABLED{'portal_fade'} ? 0 : $doFadeDefault;

    # parameters for portal.js
    $head .= qq{
        <script language="JavaScript">
            var Site;
        if (!Site)
            Site = new Object();
        Site.postUrl = "$selflink";
        Site.doFade = $doFade;
        Site.doAnimate = 1;
        </script>
        };

    # reset debug output
    $LJ::PORTAL_DEBUG_CONTENT = '';

    # do profiling? requires siteadmin portal priv
    my $profile = $GET{'profile'} + 0 && LJ::check_priv($remote, 'siteadmin', 'portal') ;
    $profile = $profile > 9999999 ? 9999999 : $profile;

    my $portalconfig = LJ::Portal::Config->new($remote, $profile);
    return unless $portalconfig; # should never happen!

    # if profiling, add debug box
    $portalconfig->add_box('Debug', 'R') if $profile;

    # see if there was a request to be handled by a module:
    my $portalboxaction = ($POST{'portalboxaction'} || $GET{'portalboxaction'}) + 0;
    my $actionbox = $portalconfig->get_box_by_id($portalboxaction);

    # verify it's a action for a valid module
    if ($portalboxaction && $actionbox && LJ::check_referer('/portal')) {
        if ($actionbox->can('handle_request')) {
            my $returncode = $actionbox->handle_request(\%GET, \%POST);
            return $returnjs->($returncode) if $returncode;

            # if it was a post then return update
            my $updatescript = LJ::Portal->get_portal_box_update_script($portalconfig, $actionbox);
            return $returnjs->($updatescript) if $POST{'portalboxaction'};
        }
    }

    # super-secret add hidden module URL option:
    if ($GET{'addmodule'}) {
        my $module = $GET{'addmodule'};
        $module =~ s/\W+//g;
        LJ::Portal->addbox($portalconfig, $module, 'R');
      }

    # firstly process any box changes:
    my @actions = ('addbox', 'configbox', 'delbox', 'getmenu', 'movebox',
                   'resetbox', 'saveconfig', 'updatebox', 'resetalldo');

    for(@actions) {

        # if a POST request then it was an XML HTTP request. Do processing and return
        # to the client info.
        # if it was a GET, then the client cannot do XML HTTP requests. Do processing
        # and display the new page.

        # did they do a POST or GET?
        my $didpost = $POST{$_} ? 1 : undef;
        my $didget  = $GET{$_}  ? 1 : undef;
        my $action  = $_;

        if (($didget || $didpost) && LJ::check_referer('/portal')) {
            # request to create a new box
            if ($action eq 'addbox') {
                my $boxtype = $POST{'boxtype'} || $GET{'boxtype'};
                my $boxcol  = $POST{'boxcol'}  || $GET{'boxcol'};

                my $returncode = LJ::Portal->addbox($portalconfig, $boxtype, $boxcol);
                return $returnjs->($returncode) if $returncode && $didpost;
            }

            # request to get a dialog box for configuring box props
            if ($action eq 'configbox') {
                my $pboxid = ($POST{'pboxid'} || $GET{'pboxid'}) + 0;
                my $jsmode = $POST{'jsmode'};

                my ($returncode, $configboxhtml) = LJ::Portal->configbox($pboxid, $portalconfig, $jsmode);

                if ($didget && $configboxhtml) {
                    # just print out the config box for non-JS people
                    my $box = $portalconfig->get_box_by_id($pboxid);
                    if ($box) {
                        $title = "Configure " . $box->box_name;
                        $body = $configboxhtml;
                        my $returnlinktext = BML::ml('.returnlink',
                                     { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
                        $body .= "<div class='PortalReturn'><b><a href=\"$LJ::SITEROOT/portal\">$returnlinktext</a></b></div>";
                    } else {
                        $title = "Error.";
                        $body = "Could not load box.";
                    }
                    return;
                }

                return $returnjs->($returncode);
            }
            # request to move a box
            if ($action eq 'movebox') {
                my $pboxid    = ($POST{'pboxid'}        || $GET{'pboxid'})+ 0;
                my $boxcol    =  $POST{'boxcol'}        || $GET{'boxcol'};
                my $boxcolpos = ($POST{'boxcolpos'}     || $GET{'boxcolpos'}) + 0;
                my $moveUp    =  $POST{'up'}            || $GET{'up'};
                my $moveDown  =  $POST{'down'}          || $GET{'down'};

                my $returncode = LJ::Portal->movebox($pboxid, $portalconfig, $boxcol,
                                                     $boxcolpos, $moveUp, $moveDown);

                return $returnjs->($returncode) if $returncode && $didpost;
            }
            # request to retreive a config menu
            if ($action eq 'getmenu') {
                my $menu = $POST{'menu'} || $GET{'menu'};

                my $returncode = LJ::Portal->getmenu($portalconfig, $menu);

                if ($didpost) {
                    return $returnjs->($returncode);
                } else {
                    if ($menu eq 'addbox') {
                        $title = 'Add Module';
                    }

                    $body = $returncode;
                    my $returnlinktext = BML::ml('.returnlink',
                                     { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
                    $body .= "<div class='PortalReturn'><b><a href=\"$LJ::SITEROOT/portal\">$returnlinktext</a></b></div>";
                    return;
                }
            }
            # reset default box settings
            if ($action eq 'resetbox') {
                my $pboxid = ($POST{'pboxid'} || $GET{'pboxid'}) + 0;
                my $returncode = LJ::Portal->resetbox($pboxid, $portalconfig);
                return $returnjs->($returncode) if $returncode && $didpost;
            }
            # request to save box props
            # only POST
            if ($action eq 'saveconfig' && $didpost) {
                my $pboxid = $POST{'pboxid'} + 0;

                # was this submitted by an actual form instead of a XML
                # HTTP request? if so then don't return XML.
                my $realform = $POST{'realform'} + 0;

                # there has got to be a better way to do this instead of passing POST
                my $returncode = LJ::Portal->saveconfig($portalconfig, $pboxid, $realform, \%POST);
                return $returnjs->($returncode) if !$realform;
            }
            # request to delete a box
            if ($action eq 'delbox') {
                my $pboxid = ($POST{'pboxid'} || $GET{'pboxid'}) + 0;
                my $returncode;

                $returncode = LJ::Portal->delbox($portalconfig, $pboxid);
                return $returnjs->($returncode) if $didpost && $returncode;
            }
            # request to get code to update the contents of a box
            # only POST
            if ($action eq 'updatebox' && $didpost) {
                my $pboxid =  $POST{'pboxid'} + 0;
                my $box = $portalconfig->get_box_by_id($pboxid);
                if ($box) {
                    return $returnjs->(LJ::Portal->get_portal_box_update_script($portalconfig, $box));
                } else {
                    return $returnjs->('alert("Box not found.");');
                }
            }
            # reset entire portal?
            # only POST
            if ($action eq 'resetalldo' && $didpost && $POST{'resetyes'}) {
                $portalconfig->reset_all;

                # tell browser to reload now that everything's finished resetting
                # (if we're using JS)
                if ($POST{'jsmode'}) {
                    return $returnjs->( qq {
                        // reload the page
                            window.location.href = Site.postUrl;
                    } );
                }
            }

            # make sure we're at /portal/ because of referral checking
            BML::redirect($selflink);
        }
    }

    # create the hidden menu at the top
    my $configmenu = qq {
        <span id="PortalConfigMenuBar">
            <div id="AddPortalMenuButton" class="midalign PortalMenuItem" onclick="return showAddPortalBoxMenu();">
            <a href="$selflink?getmenu=1&menu=addbox" class="PortalMenuItem">Add Content Module</a></div>
            </span>
        };

    # user pressed "reset all"
    # are they sure?
    if ($POST{'resetall'}) {
        my $returnlinktext = BML::ml('.returnlink',
                                     { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
        $title = BML::ml('.resetalltitle', { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
        $body .= qq {
            <p><?_ml .resetall _ml?></p>
                <div class="standout">
            <form action="$selflink" method="POST">
                <input type="hidden" name="resetalldo" value="1" />
                <input type="Submit" name="resetyes" value="Yes" />
                <input type="Submit" name="resetno" value="No" />
            </form>
            </div>
            <b><a href=\"$LJ::SITEROOT/portal\">$returnlinktext</a></b>
        };

    } elsif ($GET{'mode'} eq 'config') {

        # the config page for non-JS users
        my $returnlinktext = BML::ml('.returnlink',
                                     { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
        $title = BML::ml('.configtitle', { 'sitenameabbrev' => $LJ::SITENAMEABBREV });
        $body .= "<ul><li><a href=\"$selflink?getmenu=1&menu=addbox\"><?_ml .addbox _ml?></a></li></ul>";
        $body .= "<b><a href=\"$LJ::SITEROOT/portal\">$returnlinktext</a></b>";
    } else {
        $body .= $configmenu;

        $body .= '<div id="PortalContentContainer">';

        # print out each column with boxes
        my $cols = {};
        map { $cols->{$_} = 1 if $_ } $portalconfig->get_cols;
        my $debugbox;
        foreach my $col (keys %$cols) {
            next unless $col;

            my @colboxes = $portalconfig->get_col_boxes($col);
            $body .= "<div id='PortalCol$col'>";
            foreach my $box (@colboxes) {
                next unless $box;
                if ($box) {
                    # if it's the debug module, generate it last
                    if ($box->box_class eq 'Debug') {
                        $debugbox = $box;
                        next;
                    }

                    # get the contents of the box
                    $body .= $portalconfig->generate_box_with_container($box->pboxid);
                }
            }
            $body .= '</div>';
        }

        # print out columns that didn't have boxes so you can move boxes around
        for (('L', 'R')) {
            if (!$cols->{$_}) {
                $body .= "<div id='PortalCol$_'></div>";
            }
        }

        $body .= '<div class="clearing" />&nbsp;</div></div>';

        if ($debugbox) {
            $body .= "<div style=\"z-index: 5; position: absolute; width: 80%; height: 300; overflow-Y: auto;\"> ";
            my $debug = $portalconfig->generate_box_with_container($debugbox->pboxid);
            $body .= $debug;
            $body .= "</div>";
        }

        %LJ::PORTAL_PROFILED_BOX = ();
    }
}

#_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-

return;

_code?><?page
    title=><?_code return $title; _code?>
    head<=
    <?_c TODO: configurable meta tags could go here? _c?>
    <?_code return $head; _code?>
    <?_code return (! LJ::get_remote() &&
                    ! $LJ::IS_SSL &&
                    ! $LJ::REQ_HEAD_HAS{'chalresp_js'}++) ?
    $LJ::COMMON_CODE{'chalresp_js'} : "";
_code?>
    <=head
    bodyopts=><?_code return $bodyopts; _code?>
    body=><?_code return $body; _code?>
    page?><?_c <LJDEP>
    </LJDEP> _c?>
