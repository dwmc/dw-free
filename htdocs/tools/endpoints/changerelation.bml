<?_code # -*-bml-*-
{
    use strict;
    use vars qw(%POST);
    use LJ::Auth;
    use JSON;

    my $err = sub {
        my $msg = shift;
        return JSON::objToJson({
            error => "Error: $msg",
        });
    };

    # get user
    my $remote = LJ::get_remote()
        or return $err->("Sorry, you must be logged in to use this feature.");

    # check auth token
    return $err->("Invalid auth token") unless LJ::Auth->check_ajax_auth_token($remote, '/__rpc_changerelation', %POST);

    my $target = $POST{target} or return $err->("No target specified");
    my $action = $POST{action} or return $err->("No action specified");

    # Prevent XSS attacks
    $target = LJ::ehtml($target);
    $action = LJ::ehtml($action);

    my $targetu = LJ::load_user($target) or return $err->("Invalid user $target");

    my $success = 0;

    my %ret = ();

    if ( $action eq 'addTrust' ) {
        my $error;
        return $err->( $error )
            unless $remote->can_trust( $targetu, errref => \$error );

        $success = $remote->add_edge( $targetu, trust => {} );
    } elsif ( $action eq 'addWatch' ) {
        my $error;
        return $err->( $error )
            unless $remote->can_watch( $targetu, errref => \$error );

        $success = $remote->add_edge( $targetu, watch => {} );
    } elsif ( $action eq 'removeTrust' ) {
        $success = $remote->remove_edge( $targetu, trust => {} );
    } elsif ( $action eq 'removeWatch' ) {
        $success = $remote->remove_edge( $targetu, watch => {} );
    } elsif ( $action eq 'join' ) {
        my $error;
        my $can_join = $remote->can_join( $targetu, errref => \$error ) ? 1 : 0;
        if ( $can_join ) {
            $success = LJ::join_community( $remote, $targetu );
        } else {
            if ( $error eq LJ::Lang::ml( 'edges.join.error.targetnotopen' ) && $targetu->is_moderated_membership ) {
                LJ::comm_join_request( $targetu, $remote );
                $ret{note} = BML::ml( '/community/join.bml.reqsubmitted.body' );
            } else {
                return $err->( $error );
            }
        }
    } elsif ( $action eq 'leave' ) {
        my $error;
        return $err->( $error )
            unless $remote->can_leave( $targetu, errref => \$error );

        # log to userlog if remote is a maintainer
        $targetu->log_event( 'maintainer_remove', { actiontarget => $remote->id, remote => $remote } )
            if $remote->can_manage( $targetu );

        $success = LJ::leave_community( $remote, $targetu );
    } elsif ( $action eq 'setBan' ) {
        my $list_of_banned = LJ::load_rel_user($remote, 'B') || [ ];

        return $err->("Exceeded limit maximum of banned users")
            if @$list_of_banned >= ($LJ::MAX_BANS || 5000);

        my $ban_user = LJ::load_user($target);
        $success = $remote->ban_user($ban_user);
        LJ::run_hooks('ban_set', $remote, $ban_user);

        #$ret{is_banned} = $remote->is_banned($ban_user) ? 1 : 0;
    } elsif ( $action eq 'setUnban' ) {
        my $unban_user = LJ::load_user($target);
        $success = $remote->unban_user_multi($unban_user->{userid});
        #$ret{is_banned} = $remote->is_banned($unban_user) ? 1 : 0;
    } else {
        return $err->("Invalid action $action");
    }

    sleep 1 if $LJ::IS_DEV_SERVER;

    return JSON::objToJson({
        success     => $success,
        is_trusting => $remote->trusts( $targetu ),
        is_watching => $remote->watches( $targetu ),
        is_member   => $remote->member_of( $targetu ),
        is_banned   => $remote->has_banned( $targetu ),
        %ret,
    });
}

_code?>
