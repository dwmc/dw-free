<?_code
{
    use strict;
    BML::decl_params(user => qr/^\w{1,15}$/);

    my $remote = LJ::get_remote();
    if ($remote && $GET{'user'} eq "") {
        return BML::redirect("$LJ::SITEROOT/tools/opml.bml?user=$remote->{user}");
    }

    return "No 'user' argument" unless $GET{'user'};

    my $u = LJ::load_user($GET{'user'})
        or return "Invalid user.";

    BML::set_content_type("text/plain");
    my $x = "<?" . "xml version='1.0'?>
<opml version='1.1'><head>
";

    my $add = sub {
        my ($e, $v) = @_;
        return unless $v;
        $x .= "<$e>" . LJ::exml($v) . "</$e>\n";
    };

    $add->("title", "$u->{user}'s $LJ::SITENAME reading list");
    $add->("ownerName", $u->{name});
    $add->("ownerEmail", $u->email_visible($remote));

    $x .= "</head><body>\n";

    my $frs = LJ::get_friends($u);
    my @uids = keys %$frs;
    my $us  = LJ::load_userids(@uids);
    foreach my $uid (sort { $a <=> $b } @uids) {
        my $fr = $us->{$uid} or next;
        my $title = $fr->{name};
        $x .= "<outline text=\"" . LJ::exml($title) . "\" xmlURL=\"" . LJ::exml($fr->journal_base . "/data/rss") . "\" />\n";
    }
    $x .= "</body></opml>\n";
    BML::noparse();
    return $x;

}
_code?>
