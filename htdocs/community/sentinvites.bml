<?_c
# This code was forked from the LiveJournal project owned and operated
# by Live Journal, Inc. The code has been modified and expanded by
# Dreamwidth Studios, LLC. These files were originally licensed under
# the terms of the license supplied by Live Journal, Inc, which can
# currently be found at:
#
# http://code.livejournal.org/trac/livejournal/browser/trunk/LICENSE-LiveJournal.txt
#
# In accordance with the original license, this code and all its
# modifications are provided under the GNU General Public License.
# A copy of that license can be found in the LICENSE file included as
# part of this distribution.
_c?>
<?page
title=><?_ml .title _ml?>
body<=
<?_code
{
    use strict;
    use vars qw(%GET %POST);

    LJ::set_active_crumb('commsentinvites');

    return LJ::server_down_html() if $LJ::SERVER_DOWN;

    # always have links at top
    my $ret;

    # get remote
    my $remote = LJ::get_remote();
    unless ($remote) {
        $ret .= "<?needlogin?>";
        return $ret;
    }

    # old GET arg
    if ($GET{comm}) {
        return BML::redirect("$LJ::SITEROOT/community/sentinvites?authas=$GET{comm}");
    }

    my $cname = $GET{authas};
    return BML::redirect("$LJ::SITEROOT/community/manage") unless $cname;

    $ret .= "<form action='sentinvites' method='GET'>\n";
    $ret .= LJ::make_authas_select($remote, { authas => $GET{authas}, type => 'C' });
    $ret .= "</form>\n\n";

    # get $c object
    my $c = LJ::load_user($cname);
    unless ($c) {
        $ret .= "<?h1 $ML{'Error'} h1?><?p $ML{'/community/members.bml.error.nocomm'} p?>";
        return $ret;
    }

    # make sure it is a comm
    unless ( $c->is_community ) {
        $ret .= "<?h1 $ML{'Error'} h1?><?p ";
        $ret .= BML::ml('.error.notcomm', { 'user' => LJ::ljuser($c) });
        $ret .= " p?>";
        return $ret;
    }

    my $cid = $c->userid;
    # is $remote an admin?
    unless ( $remote->can_manage_other( $c ) ) {
        $ret .= "<?h1 $ML{'Error'} h1?><?p ";
        $ret .= BML::ml('/community/members.bml.error.noaccess',
                        { 'comm' => LJ::ljuser($cname, { 'type' => 'C' }) });
        $ret .= " p?>";
        return $ret;
    }

    $ret .= $c->maintainer_linkbar( "invites" );

    # Process revoked invitations
    if ($POST{'action:revoke'}) {
        # validate form auth
        return "<?h1 $ML{'Error'} h1?><?p $ML{'error.invalidform'} p?>"
            unless LJ::check_form_auth();

        my @userids;
        foreach (grep { /^revoke_invite/ } keys %POST) {
            push @userids, $POST{$_};
        }
        if ( $c->revoke_invites( @userids ) ) {
            $ret .= "<div class='warningbar'> " .
                    BML::ml('.invites.cancelled', { num => scalar @userids }) .
                    "</div>";
        }
    }

    # columns of our table, excluding username
    my @attribs = ('post');
    my @titleattribs = ('P');
    if ( $c->prop( 'moderated' ) ) {
        push @attribs, ('preapprove', 'moderate');
        push @titleattribs, ('U', 'M');
    }
    push @attribs, 'admin';
    push @titleattribs, 'A';

    # now get sent invites and the users involved
    my $sent = $c->get_sent_invites || [];
    my @ids;
    push @ids, ($_->{userid}, $_->{maintid}) foreach @$sent;
    my $us = LJ::load_userids(@ids);

    # populate %users hash
    my %users = ();
    my %usernames;
    foreach my $invite (@$sent) {
        my $id = $invite->{userid};
        next if $GET{show} && $GET{show} ne 'all' && $invite->{status} ne $GET{show};
        my $name = $us->{$id}{user};
        $users{$id}{userid} = $id;
        $users{$id}{maintainer} = $us->{$invite->{maintid}}{user};
        $users{$id}{user} = $name;
        $users{$id}{$_} = 1 foreach keys %{$invite->{args}};
        $users{$id}{status} = $invite->{status};
        $users{$id}{date} = LJ::mysql_time($invite->{recvtime});
        $users{$id}{date} =~ s/\s.+$//; # we just want the date, ignore time
        $usernames{$name} = $id;
    }

    # hop out if there were no invites
    unless ( %users ) {
        # display a different message depending on what category of invitations we're looking for
        return "$ret<?h1 $ML{'.none.title'} h1?><?p " .
            BML::ml( '.none.'.$GET{show}.'.body', { aopts => "href='/community/members?authas=$c->{user}'" } ) .
            " p?>"
            if ( $GET{show} eq 'accepted' || $GET{show} eq 'rejected' || $GET{show} eq 'outstanding' );

        # return with a general message if we didn't match one of the above special cases
        return "$ret<?h1 $ML{'.none.title'} h1?><?p " .
            BML::ml( '.none.body', { aopts => "href='/community/members?authas=$c->{user}'" } ) .
            " p?>";
    }

    # sorting method; default sort by username
    my $method = $GET{'sort'};
    my %sortmap = ( A      => sub { $b->{admin}      <=> $a->{admin} },
                    P      => sub { $b->{post}       <=> $a->{post} },
                    M      => sub { $b->{moderate}   <=> $a->{moderate} },
                    U      => sub { $b->{preapprove} <=> $a->{preapprove} },
                    status => sub { $a->{status}     cmp $b->{status} },
                    date   => sub { $b->{date}       cmp $a->{date} },
                    maint  => sub { $b->{maintainer} cmp $a->{maintainer} },
                  );
    my $sort = $sortmap{$method} || sub { 0 };

    # even if we're sorting another way, we still want a
    # secondary sort by username, for predictability

    my $cmp = sub { $sort->( $a, $b ) || $a->{user} cmp $b->{user} };
    my @users = sort $cmp values %users;

    # iterate through list and store position value in users hash
    for ( my $i = 0; $i < @users; $i++ ) {
        my $id = $users[$i]->{userid};
        $users{$id}->{index} = $i;
    }

    my $page_size = 100; # change to adjust page size

    # are we going to jump to a specific user ?
    my ( $jumppage, $jumpuser, $currpage );
    if ( $POST{jumpto} ) {
        if ( my $user = $users{$usernames{$POST{jumpto}}} ) {
            $jumpuser = $user->{user};
            $jumppage = $user->{index} + 1;
            $currpage = int( $user->{index} / $page_size ) + 1;
            $page_size = 1;  # just show this user
        }
    }
    $jumppage = $GET{page} unless defined $jumppage;

    # how to make links back to this page
    my $self_link = sub {
        my ( $sort, $filter ) = ( "", "" );
        $sort = "&sort=$GET{'sort'}" if $GET{'sort'};
        $filter = "&show=$GET{'show'}" if $GET{'show'};
        return "sentinvites?authas=$cname&page=$_[0]$sort$filter";
    };

    my %items = BML::paging( \@users, $jumppage, $page_size );
    my $navbar = LJ::paging_bar($items{'page'}, $items{'pages'},
                                 { 'self_link' => $self_link });
    @users = @{$items{'items'}};

    # output starts here

    # jump to user
    $ret .= "<form method='post' action='sentinvites?authas=$cname'>";
    $ret .= "<p>$ML{'/community/members.bml.jump2'}: ";
    $ret .= LJ::html_text( { name => 'jumpto', value => $POST{jumpto},
                             size => '10', maxlength => '75' } ) . " ";
    $ret .= LJ::html_submit(undef, 'Go') . "</p></form>";

    if ( $jumpuser ) {
        $ret .= "<p><h3><a href='" . $self_link->( $currpage ) . "'>";
        $ret .= "$ML{'/community/members.bml.jump.clear'}</a></h3></p>";
    } else {
        $ret .= "<p><h3>$ML{'/community/members.bml.jump.notfound'}</h3></p>"
            if $POST{jumpto};  # was not found in the list
        $ret .= $navbar;
    }

    # setup showlinks
    my $showlinks = "<form method='get' action='/community/sentinvites'>";
    $showlinks .= LJ::html_hidden('authas', $c->{user});
    $showlinks .= $ML{'.filterto'};
    $showlinks .= LJ::html_select( { name => 'show', selected => $GET{show} }, all => $ML{'.filterto.opts.all'}, accepted => $ML{'.filterto.opts.accepted'}, rejected => $ML{'.filterto.opts.rejected'}, outstanding => $ML{'.filterto.opts.outstanding'} );
    $showlinks .= LJ::html_submit( undef, $ML{'.filterto.btn.refresh'} );
    $showlinks .= "</form>";

    my $filter = $GET{show} ? "&show=$GET{show}" : "";
    my $sortlink = BML::get_uri() . "?authas=$cname$filter&sort=";
    $ret .= "<br /><div align='center'>$showlinks";
    $ret .= <<END;
<table class='borderedtable' cellpadding='2' cellspacing='0'>
<tr style="border-bottom: solid 1px black;">
    <th style='border-right: solid 1px black;'>$ML{'.key.posting.abbrev'}</th>
    <td style='border-bottom: solid 1px black; border-right: solid 1px black;'>$ML{'.key.posting'}</td>
    <th style='border-right: solid 1px black;'>$ML{'.key.unmoderated.abbrev'}</th>
    <td style='border-bottom: solid 1px black;'>$ML{'.key.unmoderated'}</td>
</tr>
<tr style="border-bottom: solid 1px black;">
    <th style='border-right: solid 1px black; border-bottom: none;'>$ML{'.key.moderator.abbrev'}</th>
    <td style='border-right: solid 1px black;'>$ML{'.key.moderator'}</td>
    <th style='border-right: solid 1px black; border-bottom: none;'>$ML{'.key.maintainer.abbrev'}</th>
    <td>$ML{'.key.maintainer'}</td>
</tr>
</table>
<br />
END
    $ret .= "<table cellpadding='0' cellspacing='0'>\n<thead><tr>" .
            "<th><a href='${sortlink}name'>$ML{'/community/members.bml.key.user'}</a></th>";
    $ret .= "<th><a href='${sortlink}$_'>$_</a></th>" foreach @titleattribs;
    $ret .= "<th><a href='${sortlink}maint'>$ML{'.key.sentby'}</a></th>";
    $ret .= "<th><a href='${sortlink}date'>$ML{'.key.date'}</a></th>";
    $ret .= "<th><a href='${sortlink}status'>$ML{'.key.status'}</a></th>";
    $ret .= "</tr></thead>\n";

    $ret .= "<form method='post'>";
    $ret .= LJ::form_auth();
    my $can_revoke = 0;

    # checkboxes and such
    my $yes = LJ::img( 'check', '' );
    my $no = '-';

    # rows for existing users
    my $rc = 0;
    foreach(@users) {
        my $rstyle = ($rc++ & 1) ? "even" : "odd";
        $ret .= "<tr class='$rstyle'><td>" . LJ::ljuser($_->{user}) . "</td>";
        foreach my $key (@attribs) {
            $ret .= "<td align='center'>";
            $ret .= $_->{$key} ? $yes : $no;
            $ret .= "</td>";
        }
        $ret .= "<td>" . LJ::ljuser($_->{maintainer}, { type => 'P' }) . "</td>";
        $ret .= "<td>$_->{date}</td>";

        # display checkbox for outstanding invites so they can be revoked
        # also set flag indicating there are invitations that can be revoked
        if ($_->{status} eq 'outstanding') {
            $can_revoke = 1;
            $ret .= "<td>$_->{status} " .
                    LJ::html_check({ 'type' => 'checkbox',
                                     'name' => 'revoke_invite_' . $_->{userid},
                                     'value' => $_->{userid} }) .
                    " </td>";
        } else {
            $ret .= "<td>$_->{status}</td>";
        }


        $ret .= "</tr>\n";
    }

    $ret .= "</table>\n";
    $ret .= "<p>" . LJ::html_submit( 'action:revoke', $ML{'.cancel'}) .  "</p>"
        if $can_revoke;
    $ret .= "</form>";


    $ret .= "<br /><a href='/community/members?authas=$c->{user}'>$ML{'.send'}</a>";
    $ret .= "</div>\n\n";
    $ret .= $navbar unless $jumpuser;
    return $ret;

}
_code?>

<=body
page?>
