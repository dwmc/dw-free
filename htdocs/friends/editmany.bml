<?page
body<=
<?_code
{
    use strict;
    use vars qw(%GET %POST $title $headextra @errors @warnings);

    LJ::need_res(qw(
                    js/editfriendsmany.js
                    ));

    $title = $ML{'.title'};

    my $remote = LJ::get_remote() or return "<?needlogin?>";
    my $u;
    my $username = $POST{user} || $GET{user} || '';
    $u = LJ::load_user($username) if $username;

    # used in multiple places
    my $groupsinfo = LJ::get_friend_group($remote);
    my @groupids = sort {
        $groupsinfo->{$a}->{sortorder} <=> $groupsinfo->{$b}->{sortorder}
    } keys %$groupsinfo;

    my $err;
    if ($u && LJ::did_post()) {
        # process edits:
        if ($POST{friend}) {
            # process friend groups
            if ($remote->is_friend($u) || $remote->can_add_friends(\$err, {friend => $u})) {
                my $groupmask = 1;
                foreach my $fgroupid (@groupids) {
                    $groupmask |= 1 << $fgroupid if $POST{"fgroup$fgroupid"};
                }
                $remote->add_friend($u, {groupmask => $groupmask});
            }
        } else {
            $remote->remove_friend($u);
        }
    }

    # clean user input for display
    $username = $u->user if $u;

    my $ret;
    $ret .= LJ::error_list($err) if $err;

    $ret .= qq {
        <fieldset>
            <form method="GET" id="chooseuserform">
                $ML{'.edit.user'} <input name="user" type="text" maxlength="15" size="15" id="user" value="$username" />
                <input type="submit" value="$ML{'.edit'}" />
            </form>
        </fieldset>
    };

    if ($u) {
        my $upic = $u->userpic;
        my $ljuser = $u->ljuser_display;
        my $uname = $u->name_html;
        my $userpictag = $upic ? '<img src="' . $upic->url . '"/>' : '';

        # can't use LJ::is_friend because that returns true if $remote == $u
        my $is_friend = LJ::get_groupmask($remote->id, $u->id) & 1;

        my $friendcheck = LJ::html_check({
            id       => "friend",
            name     => "friend",
            selected => $is_friend,
            label    => "Friend",
        });

        my $friendgroups = '';
        # build friend group checkboxes
        foreach my $groupid (@groupids) {
            my $groupinfo = $groupsinfo->{$groupid};

            # is the user in this friend group?
            # may need to force master?
            my $in_group = $remote->user_in_friend_group($u, $groupid);

            my $groupcheck = LJ::html_check({
                name  => "fgroup$groupid",
                id    => "fgroup$groupid",
                class => "fgroup",
                label => LJ::ehtml($groupinfo->{groupname}),
                selected => $in_group,
                disabled => ! $is_friend,
            });

            $friendgroups .= qq {<div class="friendgroup">$groupcheck</div>};
        }

        my $relation = '';
        if ($remote->id == $u->id) {
            $relation = '';
        } elsif ($remote->is_friend($u)) {
            if ($u->is_friend($remote)) {
                $relation = BML::ml('.you.are.mutual', {"username"=>$username});
            } else {
                $relation = BML::ml('.is.your.friends', {"username"=>$username});
            }
        } else {
            if ($u->is_friend($remote)) {
                $relation = BML::ml('.is.friends.with.you', {"username"=>$username});
            } else {
                $relation = BML::ml('.you.are.not.friends', {"username"=>$username});
            }
        }

        $ret .= qq {
            <fieldset>
                <form method="POST">
                    <div id="userinfo_container">
                        <div class="userpic">$userpictag</div>
                        <div class="userinfo">
                            <div class="user">$ljuser</div>
                            <div class="username">$uname</div>
                            <div class="relation">$relation</div>
                        </div>
                        <div class="ljclear"></div>
                        $friendcheck
                    </div>
                    <div id="friendgroups">
                        $friendgroups
                    </div>
                    <input type="submit" id="save" value="$ML{'.save'}" />
                </form>
            </fieldset>
        };
    }

    return $ret;
}
_code?>
<=body
title=><?_code return $title; _code?>
head<=
<?_code return $headextra; _code?>
<style type="text/css">
div.userpic {
    float: left;
}
div.userinfo {
}
div.user {
    font-weight: bold;
}
div.username {
    font-style: italic;
}
</style>
<=head
page?>
