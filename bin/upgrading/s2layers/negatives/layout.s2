layerinfo type = "layout";
layerinfo name = "Negatives";
layerinfo redist_uniq = "negatives/layout";
layerinfo author_name = "phoenix";
layerinfo des = "Highly customisable div-based layout";
layerinfo lang = "en";

##===============================
## Display settings - general
##===============================

propgroup presentation {
    property use num_items_recent;
    property use num_items_reading;
    property use use_journalstyle_entry_page;
    property use layout_type;
    property use tags_page_type;
    property use use_custom_friend_colors;
}

set layout_type = "two-columns-right";
set custom_colors_template = "%%new%% div.footer, %%new%% div.footer ul, %%new%% div.footer ul a {background-color: %%background%%} %%new%% {border: 5px solid %%background%%;} %%new%% div.footer a, %%new%% div.footer {color: %%foreground%%;}";

##===============================
## Journal style - colors
##===============================

propgroup colors {

    property use color_page_background;
    property use color_page_text;
    property use color_page_link;
    property use color_page_link_hover;

    property use color_module_background;
    property use color_module_text;
    property use color_module_border;
    property use color_module_link;
    property use color_module_link_hover;
    property use color_module_title;

    property use color_entry_link;
    property use color_entry_link_hover;
    property use color_entry_border;
    property use color_entry_background;
    property use color_entry_text;
    property use color_entry_title;
    property use color_entry_interaction_links;

}

set color_page_background = "#000000";
set color_page_text = "#ffffff";
set color_page_link = "#cccccc";
set color_page_link_hover = "#ffffff";

set color_entry_link = "#000000";
set color_entry_link_hover = "#cccccc";
set color_entry_link_visited = "#000000";
set color_entry_border = "#000000";
set color_entry_background = "#ffffff";
set color_entry_text = "#000000";
set color_entry_title = "#000000";
set color_entry_interaction_links = "#ffffff";

set color_module_background = "#000000";
set color_module_text = "#cccccc";
set color_module_border = "#aaaaaa";
set color_module_title = "#cccccc";
set color_module_link = "#cccccc";
set color_module_link_active = "#222222";
set color_module_link_hover = "#ffffff";

##===============================
## Journal style - images
##===============================

propgroup images {
    property use image_background_page_group;
    property use image_background_header_group;
    property use image_background_header_height;
    property use image_background_entry_group;
    property use image_background_module_group;
}

##===============================
## Display settings - modules
##===============================

propgroup modules {
    property use module_userprofile_group;
    property use module_navlinks_group;
    property use module_customtext_group;
    property use module_calendar_group;
    property use module_pagesummary_group;
    property use module_tags_group;
    property use module_links_group;
    property use module_syndicate_group;
    property use module_time_group;
    property use module_poweredby_group;
}


set module_layout_sections = "none|(none)|one|Sidebar|two|Header|three|Footer";

set module_navlinks_section = "two";

set module_userprofile_section = "one";
set module_customtext_section = "one";
set module_pagesummary_section = "one";
set module_tags_section = "one";
set module_links_section = "one";
set module_syndicate_section = "one";

set module_calendar_section = "three";
set module_calendar_opts_type = "horizontal";

set module_time_section = "none";
set module_poweredby_section = "one";

##===============================
## Custom CSS
##===============================

propgroup customcss {
    property use external_stylesheet;
    property use include_default_stylesheet;
    property use linked_stylesheet;
    property use custom_css;
}

propgroup text {

##===============================
## Text - main navigation
##===============================

    property use text_view_archive;
    property use text_view_userinfo;
    property use text_view_memories;
    property use text_view_tags;

##===============================
## Text - entry comment-related
##===============================

    property use text_view_recent;
    property use text_view_friends;
    property use text_view_friends_comm;
    property use text_view_friends_filter;
    property use text_view_archive;
    property use text_view_userinfo;
    property use text_view_memories;

    property use text_post_comment;
    property use text_max_comments;
    property use text_read_comments;
    property use text_post_comment_friends;
    property use text_read_comments_friends;

    property use text_skiplinks_back;
    property use text_skiplinks_forward;
    property use text_meta_music; 
    property use text_meta_mood; 
    property use text_meta_location; 
    property use text_tags; 

    property use text_entry_prev;
    property use text_entry_next; 
    property use text_edit_entry; 
    property use text_edit_tags; 
    property use text_tell_friend; 
    property use text_mem_add; 
    property use text_watch_comments; 
    property use text_unwatch_comments; 
    property use text_permalink;

    property use text_module_customtext;
    property use text_module_customtext_content;
    property use text_module_customtext_url;
}

set font_base ="Georgia";
set font_fallback = "serif";
set entry_management_links = "text";
set comment_management_links = "text";

#######################
#Functions
#######################

########################
#Prints the stylesheet
function Page::print_default_stylesheet()
{
    var string page_background = generate_background_css($*image_background_page_url,$*image_background_page_repeat,$*image_background_page_position,$*color_page_background);
    var string header_background = generate_background_css($*image_background_header_url, $*image_background_header_repeat, $*image_background_header_position, new Color);
    var string entry_background = generate_background_css ($*image_background_entry_url, $*image_background_entry_repeat, $*image_background_entry_position,$*color_entry_background);
    var string module_background = generate_background_css ($*image_background_module_url, $*image_background_module_repeat, $*image_background_module_position,$*color_module_background);
    if ($*image_background_header_height > 0) {
        $header_background = $header_background + "\n        height: " + $*image_background_header_height + "px;";
    }

    """<style type="text/css">""";
    start_css();
"""
    body {
        margin: 0;
        padding: 0;
        font-family: $*font_base, $*font_fallback;
        font-size: 1em;
        $page_background
        color:$*color_page_text;
        }
    a {
        text-decoration: none;
        font-weight: bold;
        color:  $*color_page_link;
        outline: none;
        }
    a:visited, a:active {
        color: $*color_entry_link_active;
        }
    a:hover {
        color: $*color_page_link_hover;
        }
    img, img a {
        border: 0!important;
        }
    #header {
        border-top: 0!important;
        border-bottom: 5px double $*color_module_border;
        $header_background
    }
    .module-section-two {
    }
    .module-section-three {
        border-bottom: 0!important;
        border-top: 5px double $*color_module_border;
    }
    #footer {
        clear:both;
    }
    #primary {
        position: relative;
        float: left;
        font-size: 0.9em;
        padding: 10px;
        padding-bottom: 5px;
        border: 0px;
        border-left:5px double $*color_module_border;
        border-right:5px double $*color_module_border;
        left: 10%;
        width: 70%;
        }
    #secondary {
        position: relative;
        float: left;
        left: 10%;
        margin: 0px;
        padding: 10px;
        border: 0px;
        padding-bottom: 10px;
        width: 15%;
        font-size: 0.80em;
        $module_background
        color: $*color_module_text;
        }
    #secondary a, #secondary a:visited {
        color:$*color_module_link;
        font-weight:normal;
        padding-bottom:1px;
    }
    #secondary a:hover, #secondary a:active {
        color:$*color_module_link_hover;
    }
    #secondary .module-header {
        border-bottom:1px solid $*color_module_border;
        font-weight:bold;
        letter-spacing:.3em;
        font-size: 1.1em;
        text-align: right;
    }
    #secondary .module-header a {
        color: $*color_module_title;
    }
    #secondary .manage-tags-link {
        text-align: center;
    }
    .module {
        letter-spacing:.3em;
        padding: 0.5em 0;
    }
    .userpic{
        float:right;}
    .entry, .entry-wrapper, .comment, .comment-wrapper {
        min-height: 110px;
        border:5px solid $*color_entry_border;
        padding: 5px;
        $entry_background;
        color: $*color_entry_text;
    }
    .comment-wrapper.partial , .partial .comment { 
        min-height: 0;
    }
    .partial .comment-title {
        display: inline;
    }
    .entry a, .comment a {
        color: $*color_entry_link;
    }
    .entry a:hover, .comment a:hover {
        color: $*color_entry_link_hover;
    }
    .tag { margin: 10px 0; }
    .tag ul { display: inline; margin: 0; padding: 0; }
    .tag li { display: inline; }
    .bottomcomment {
        border: 15px double $*color_entry_border;
        padding: 5px;
        margin-top: 5px;
        background-color: $*color_entry_background;
        color: #000000;
    }
    .entry-wrapper .footer {
        background-color: $*color_page_background;
    }
    .comment-poster .anonymous {
        font-weight: bold;
    }
    .comment-management-links {
        display: inline;
        padding-left: 0;
    }
    .entry-title, .userlite-interaction-links li,
    .entry-management-links li, .entry-interaction-links li,
    .comment-interaction-links li, .comment-management-links li {
        display: inline;
        padding-left: 0.5em;
    }
    .entry-title a {
        color: $*color_entry_title;
    }
    .entry-title a:hover {
        color: $*color_entry_link_hover;
    }
    .entry-management-links, .entry-management-links a,
    .entry-interaction-links, .entry-interaction-links a,
    .comment-interaction-links, .comment-interaction-links a {
        background-color: $*color_page_background;
        color: $*color_entry_interaction_links;
        padding: 0 0.5em;
        font-weight: normal;
        text-align: center;
    }
    .entry-management-links a:hover .comment-management-links a:hover, 
    .entry-interaction-links a:hover .comment-interaction-links a:hover{
        color: $*color_entry_link_hover;
    }
    .userlite-interaction-links ul, .entry-management-links ul, .comment-management-links ul {
        list-style: none;
	margin-left: 0;
	padding-left: 0;
    }
   .module-navlinks ul, .module-navlinks li {
        font-weight: bold;
        display: inline;
        padding: 0.2em 1em 0.2em 2em;
     }
    .module-photo {
        text-align: center;
    }
    .module ul {
        list-style-position: inside;
    }
    .module-section-one ul { padding-left: 0.3em }
    .navigation ul { padding-left: 0em; list-style-type: none; }
    .navigation li { padding-left: 0.3em; display: inline; }
    .datetime{
        margin-bottom:10px;
        font-style:italic;
    }
    .subject, .subject a, .subject a:visited, .subject a:hover, .subject a:active {
        font-weight:bold;
        font-size:1.1em;
    }
    .module-section-two .module-calendar,
    .module-section-three .module-calendar {
        text-align: center;
        font-size: 0.8em;
        padding:5px;
        margin: 0px 190px 0px 100px;
    }
    .module-calendar table {
        letter-spacing: 0.2em;
        text-align: center;
    }
    .module-calendar {
        color: $*color_module_text;
    }
    .module-calendar a{
        color: $*color_module_link;
    }
    .module-calendar a:hover{
        color: $*color_module_link_hover;
    }

    .floatclear {
        visibility: hidden;
        clear:both;
        height:0;
    }

    /*separators*/
    span.invis{
        display:none;
    }
    /* correct LJ breaking the comment page */
    textarea.textbox{
        width: 100%;
    }
    q{
        font-style: italic;
    }
    
    .entry h3 { display: inline; }

    """;
    end_css();
    "</style>\n";
}

#Print_body
function Page::print_entry (Entry e)
{
    $e->print_wrapper_start();
    print "<div class=\"header\">";
    $e->print_userpic();
    print "<div class=\"subject\">&raquo;";
            $e->print_metatypes();
            $e->print_subject();
        println "</div>";
        print "<div class=\"datetime\">(";
            $e->print_poster();
            print " ";
            $e->print_time();
            print ")";
        println "</div>";
    println "</div>";
    $e->print_text();
    $e->print_metadata();
    $e->print_tags();
    println "<div class=\"footer\">";
    $e->print_interaction_links( "topcomment" );
    $e->print_management_links();
    println "</div>";
    $this->print_reply_container({"target" => "topcomment" });
    $e->print_wrapper_end();
}

#######################################
#Year Page
function YearPage::print_body()
{
    println "<div class=\"entry\">";

        "<div class=\"subject\">";
            $this->print_year_links();
        println "</div>";

        println "<center>";
        foreach var YearMonth m ($.months) {
            $this->print_month($m);
        }
        println "</center>";

    println "</div>";
}

function YearPage::print_month(YearMonth m) {
    if (not $m.has_entries) { return; }
    """<br /><div class="entry"><table style="width: 100%; border-collapse: collapse;">\n
       <tr><th class="comments" colspan="7" style="text-align: center;">""";
    print $m->month_format();
    """</th></tr>\n""";
    foreach var int d (weekdays()) {
        "<th>"+$*lang_dayname_short[$d]+"</th>\n";
    }
    "</tr>\n";
    foreach var YearWeek w ($m.weeks) {
        $w->print();
    }
    """<tr><td class="comments" colspan="7" style="text-align: center;">
        &laquo; <a href="$m.url">$*text_view_month</a> &raquo;</td></tr>\n""";
    "</table></div>";
}



function YearWeek::print() {
    """<tr valign="top" style="height: 2em;">\n""";
    if ($.pre_empty > 0) {
        """<td class="emptyday" colspan="$.pre_empty">&nbsp;</td>\n""";
       }
    foreach var YearDay d ($.days) {
        """<td class="entry" width="14%">\n""";
        """<div style="text-align: right;">$d.day</div>\n""";
        if ($d.num_entries > 0) {
            """<div style="text-align: center;"><a href="$d.url">$d.num_entries</a></div>\n""";
        }
    """</td>\n""";
    }
    if ($.post_empty > 0) {
        """<td colspan="$.post_empty">&nbsp;</td>\n""";
    }
    "</tr>";
}



function MonthPage::print_body
{
    println "<div class=\"entry\">";
    println "<div class=\"subject\">" + $.date->date_format($*lang_fmt_month_long) + "</div>";
    "<form method='post' action='$.redir.url'><center>";
    $.redir->print_hiddens();
    if ($.prev_url != "") { "[<a href='$.prev_url'>&lt;&lt;&lt;</a>]\n"; }
    if (size $.months > 1) {
        "<select name='redir_key'>\n";
        foreach var MonthEntryInfo mei ($.months)
        {
            var string sel;
            if ($mei.date.year == $.date.year and $mei.date.month == $.date.month) {
                $sel = " selected='selected'";
            }
            println "<option value='$mei.redir_key'$sel>" + $mei.date->date_format($*lang_fmt_month_long) + "</option>";
        }
        println "</select>\n<input type='submit' value='View' />";
    }
    if ($.next_url != "") { "\n[<a href='$.next_url'>&gt;&gt;&gt;</a>]\n"; }
    "</center></form>";
    foreach var MonthDay d ($.days) {
        if ($d.has_entries) {
            println "<div class='subject'>";
            println "<a href=\"$d.url\">" + lang_ordinal($d.day) + " " + $.date->date_format($*lang_fmt_month_long) + "</a></div>";
            println "<div style='margin-left: 20px;'>";
            $d->print_subjectlist();
            println "</div>\n<br />\n";
        }
    }
    println "</div>";
}

function DayPage::print_body()
{
    if (not $.has_entries) {
        println $*text_noentries_day;
    }
    foreach var Entry e ($.entries) {
        $this->print_entry($e);
    }
}


#####################################
#Comment Links

function EntryPage::print_comment (Comment c)
{
    $c->print_wrapper_start();
    $c->print_userpic();
    println "<div class='datetime'><a href='$c.permalink_url' title='Permalink'>";
    $c->print_time();
    println "</a>";
    print "</div>";

    if ($this.multiform_on)
    {
        """<span class="multiform-checkbox">""";
        println "<label for='ljcomsel_$c.talkid'>$*text_multiform_check</label>";
        $c->print_multiform_check();
        "</span>";
    }

    $c->print_management_links();
    println "<br />";

    $c->print_metadata();

    println "";

    $c->print_poster();
    $c->print_metatypes();

    $c->print_subject();

    println "<br />";

    $c->print_text();
    println "<div class=\"footer\">";
        $c->print_interaction_links();
    println "</div>";

    $c->print_reply_container();
    $c->print_wrapper_end();
}

####################################
#Layout Frame
function Page::print()
{
    println """<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
    <head>""";
        $this->print_head();
        $this->print_stylesheets();
        $this->print_head_title();
    println "</head>";

        $this->print_wrapper_start();
        $this->print_control_strip();
        println "<div id=\"header\">";
        $this->print_module_section("two");
        println "</div>";
        println "<div id=\"primary\">";
            $this->print_body();
            println "<div class=\"floatclear\">.</div>";
        println "</div>";

        println "<div id=\"secondary\">";
            if ($this.view != "archive" and $this.view != "month"){
                println "<div id='page-navigation-top' class='page-navigation'>";
                    $this->print_navigation();
                println "</div>";
            }
            $this->print_module_section("one");
        println "</div>";
        println "<div id=\"footer\">";
        $this->print_module_section("three");
        println "<div>";

    $this->print_wrapper_end();
    println "</html>";
}

