#!/usr/bin/perl

use strict;
use lib "$ENV{LJHOME}/cgi-bin";

require 'ljlang.pl';
require 'ljlib.pl';

use Data::Dumper;

# this script handles sending expiration notices for users as well as handling the actual
# expirations.  it should be run at least once a day but can be run more often if you want
# to have greater granularity on the expiration of accounts.

my $note = sub {
    my $u = shift;
    LJ::statushistory_add( $u, undef, 'paidstatus', sprintf('Expiration: ' . shift, @_) );
};

# 1) let's grab people expiring in the next 14 days
print "Searching for expirations in the next 14 days.\n";
my $dbh = DW::Pay::get_db_writer()
    or die "unable to get db\n";
my $now = $dbh->selectrow_array( 'SELECT UNIX_TIMESTAMP()' );
my $people = $dbh->selectall_arrayref( q{
        SELECT userid, typeid, expiretime, permanent, lastemail
        FROM dw_paidstatus
        WHERE permanent = 0
          AND expiretime IS NOT NULL
          AND expiretime < UNIX_TIMESTAMP(DATE_ADD(NOW(), INTERVAL 14 DAY))
          AND typeid > 0
          AND ( lastemail IS NULL OR lastemail != 0 )
    } );
die $dbh->errstr if $dbh->err;

# XXX: lastemail is a little lame, we use it to record which mails we have sent the user.
# since the user SHOULD always rotate through the statuses in a nice predictable fashion
# then this should work well enough.

# 2) now iterate over this list and do stuff with it
print "Got " . scalar( @$people ) . " records.\n";
foreach my $user ( @$people ) {
    my ( $uid, $typeid, $expiretime, $perm, $lastemail ) = @$user;

    if ( $expiretime < $now ) {
        if ( ! defined $lastemail || $lastemail != 0 ) {
            handle_expired( $uid );
        }

    } elsif ( $expiretime < ( $now + 3600*24*3 ) ) {
        # 3-day expiration, lastemail should be something other than 3
        if ( $lastemail != 3 ) {
            send_3day_warning( $uid );
        }

    } elsif ( $expiretime < ( $now + 3600*24*14 ) ) {
        if ( $lastemail != 14 ) {
            send_14day_warning( $uid );
        }
    }
}
print "Done.\n";

sub handle_expired {
    my $u = LJ::load_userid( shift )
        or die;

    print "$u->{user}($u->{userid}) gets expired\n";
    $note->( $u, "paid account has expired" );
    DW::Pay::update_paid_status( $u, lastemail => 0 );
    DW::Pay::sync_caps( $u );

    LJ::send_mail( {
            to => $u->email_raw,
            from => $LJ::BOGUS_EMAIL,
            subject => "$LJ::SITENAME Account Expired",
            body => <<EOF,
Dear $u->{user},

Your paid account status with $LJ::SITENAME has expired.  We hope that
this is just an oversight, and would like to encourage you to come back
and continue to support this site!

You may renew your account here:

    $LJ::SITEROOT/paidaccounts/

Thank you for your support!


Sincerely,
The $LJ::SITENAME Team
EOF
        } );
}

sub send_3day_warning {
    my $u = LJ::load_userid( shift )
        or die;

    print "$u->{user}($u->{userid}) gets a 3 day expiration mailing\n";
    $note->( $u, "sending 3 day expiration warning" );
    DW::Pay::update_paid_status( $u, lastemail => 3 );

    LJ::send_mail( {
            to => $u->email_raw,
            from => $LJ::BOGUS_EMAIL,
            subject => "$LJ::SITENAME Account Expiration in 3 Days",
            body => <<EOF,
Dear $u->{user},

Your paid account status with $LJ::SITENAME is currently only 3 days from
expiring and lapsing back to a free account.  Don't let this opportunity to
continue supporting this site slip you by!  Continue to enjoy the extra
features of a paid account by renewing now.

You may renew your account here:

    $LJ::SITEROOT/paidaccounts/

Thank you for your support!


Sincerely,
The $LJ::SITENAME Team
EOF
        } );
}

sub send_14day_warning {
    my $u = LJ::load_userid( shift )
        or die;

    print "$u->{user}($u->{userid}) gets a 14 day expiration mailing\n";
    $note->( $u, "sending 14 day expiration warning" );
    DW::Pay::update_paid_status( $u, lastemail => 14 );

    LJ::send_mail( {
            to => $u->email_raw,
            from => $LJ::BOGUS_EMAIL,
            subject => "$LJ::SITENAME Account Expiration in 14 Days",
            body => <<EOF,
Dear $u->{user},

Your paid account status with $LJ::SITENAME is currently only 14 days from
expiring and lapsing back to a free account.  Don't let this opportunity to
continue supporting this site slip you by!  Continue to enjoy the extra
features of a paid account by renewing now.

You may renew your account here:

    $LJ::SITEROOT/paidaccounts/

Thank you for your support!


Sincerely,
The $LJ::SITENAME Team
EOF
        } );
}
